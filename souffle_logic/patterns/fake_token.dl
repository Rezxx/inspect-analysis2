#if DEBUG
	.output PossibleFakeToken
	.output CheckFakeToken
#endif

.output FakeTokenViolation


/* Issues:
1. Actions named "transfer" are identified as an on_notify action, 
which triggers the pattern.

*/



.decl PossibleFakeToken(func: Function)

PossibleFakeToken(action):-
	OnTransferAction(call, action),
	!OnTransferEOSAction(call, action).

.decl CheckFakeToken(insn: Insn)

// Check if _code is used for testing
// can restrict it to only "EOSIO.token"
CheckFakeToken(insn):-
	(_AssignType(insn, to, "Ne");
	_AssignType(insn, to, "Eq")),
	_AssignVar(insn, to, from),
	pointer.VarPointsTo(from, "_code").



.decl SafeToken(action: Function)

SafeToken(action):-
	PossibleFakeToken(action),
	_FuncInsn(action, insn),
	CheckFakeToken(insn).

SafeToken(action):-
	PossibleFakeToken(action),
	ActionFuncReachable(action, func, _),
	_FuncInsn(func, insn),
	CheckFakeToken(insn).

.decl FakeTokenViolation(action: Function)
FakeTokenViolation(action):-
	PossibleFakeToken(action),
	!SafeToken(action).
